buildscript {
	ext {
		springBootVersion = '1.3.6.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id "com.moowork.node" version "0.12"
}


apply plugin: 'java'
apply plugin: 'spring-boot'

repositories {
	mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

task wrapper(type: Wrapper) {
	gradleVersion = '2.2'
}

node {
	version = '6.2.2'
	download = true
}


clean {
	doLast {
		exec { commandLine "rm", "-r", "-f", "node_modules" }
	}
}

compileJava.dependsOn "npm_install"

if(System.getenv('NODE_ENV') == "production") {
	compileJava.dependsOn "npm_run-script_package"
} else {
	compileJava.dependsOn "npm_run-script_package-dev"
}

test.dependsOn "npm_run-script_lint"
test.dependsOn "npm_test"

jar {
	baseName = 'boot-react'
	version = '0.0.1-SNAPSHOT'
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-devtools')
	compile('org.projectlombok:lombok:1.16.6')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-thymeleaf')
	compile('org.springframework.boot:spring-boot-starter-web')
	runtime('mysql:mysql-connector-java')
	testCompile('org.springframework.boot:spring-boot-starter-test') 
}


